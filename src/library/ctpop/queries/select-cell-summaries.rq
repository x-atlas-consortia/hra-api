PREFIX ccf: <http://purl.org/ccf/>
PREFIX ctpop: <https://purl.humanatlas.io/graph/ctpop>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREfIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?cell_source ?cell_source_label ?modality ?cell_id ?percentage
WHERE {
  GRAPH ctpop: {
    SELECT DISTINCT ?cell_source
    WHERE {
      #{{VALUES}}
      ?cell_source ccf:has_cell_summary [
        ccf:has_cell_summary_row [
          ccf:cell_id ?cell_id ;
        ] ;
      ] .
    }
  }
  GRAPH ctpop: {
    ?cell_source ccf:has_cell_summary [
        ccf:has_cell_summary_row [
          ccf:cell_id ?cell_id ;
          ccf:cell_label ?cell_label ;
          ccf:percentage_of_total ?percentage ;
        ] ;
      ] .
  }
  OPTIONAL {
    ?cell_source ccf:has_cell_summary [
      ccf:modality ?modality ;
    ] .
  }
  OPTIONAL {
    ?cell_source rdfs:label ?cell_source_label .
  }

  #{{ORGAN_IRIs}}

  # Anatomical Structure cell sources
  {
    [] ccf:representation_of ?cell_source ;
      ccf:has_reference_organ ?refOrgan .
  }
  UNION 
  # Dataset cell sources
  {
    {
      ?sample ccf:generates_dataset ?cell_source .
    } UNION {
      [] ccf:subdivided_into_sections ?sample .
      ?sample ccf:generates_dataset ?cell_source .
    }
    ?sample ccf:has_registration_location ?rui_location .

    [] a ccf:SpatialPlacement ;
      ccf:placement_for ?rui_location ;
      ccf:placement_relative_to ?refOrgan .
  }
  UNION
  # RUI Location cell sources
  {
    [] a ccf:SpatialPlacement ;
      ccf:placement_for ?cell_source ;
      ccf:placement_relative_to ?refOrgan .
  }

  {
    ?refOrgan ccf:representation_of ?organ_iri .
  }
  UNION
  {
    ?refOrgan ccf:part_of* ?organ_iri
  }
}
ORDER BY ?modality DESC(?similarity)