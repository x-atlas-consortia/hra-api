PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX cell: <http://purl.obolibrary.org/obo/CL_0000000>
PREFIX CCF: <https://purl.humanatlas.io/graph/ccf>
PREFIX hasExactSynonym: <http://www.geneontology.org/formats/oboInOwl#hasExactSynonym>

CONSTRUCT {
  cell: rdfs:label ?cellLabel .
  cell: rdf:type ccf:OntologyTreeNode .
  cell: ccf:is_root_for "AS" .
  ?child rdf:type ccf:OntologyTreeNode .
  ?child ccf:parent ?parent .
  ?child rdfs:label ?label .
  ?child ccf:synonymLabels ?synonymLabel .
}
FROM CCF:
WHERE {
  cell: ^ccf:ccf_ct_isa* ?child .
  cell: rdfs:label ?cellLabel .
  ?child ccf:ccf_ct_isa ?parent .
  ?child ccf:ccf_pref_label ?label1 .
  OPTIONAL {
    ?child hasExactSynonym: ?synonymLabel1 .
  }
  FILTER (!isBlank(?child) && !isBlank(?parent))

  BIND(STR(?label1) as ?label)
  BIND(STR(?synonymLabel1) as ?synonymLabel)
}
