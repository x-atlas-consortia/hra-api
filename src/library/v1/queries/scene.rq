PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX CCF: <https://purl.humanatlas.io/graph/ccf>
PREFIX DSGraphs: <https://purl.humanatlas.io/collection/ds-graphs>

CONSTRUCT {
  ?block rdf:type ccf:SpatialSceneNode ;
    ccf:has_registration_location ?rui_location ;
    ccf:entityId ?block ;
    ccf:collides_with ?anatomical_structure ;
    ccf:x_dimension ?x_dimension ;
    ccf:y_dimension ?y_dimension ;
    ccf:z_dimension ?z_dimension ;
    ccf:dimension_unit ?dimension_units .
}
FROM CCF:
FROM DSGraphs:
WHERE {
  {
    ?block ccf:comes_from ?donor .
    ?block ccf:has_registration_location ?rui_location .
    {
      ?block ccf:generates_dataset ?dataset .
    }
    UNION
    {
      ?block ccf:subdivided_into_sections ?section .
      ?section ccf:generates_dataset ?dataset .
    }
    OPTIONAL {
      ?donor ccf:consortium_name ?consortium .
    }
    OPTIONAL {
      ?donor ccf:tissue_provider_name ?provider .
    }
    OPTIONAL {
      ?donor ccf:age ?age .
    }
    OPTIONAL {
      ?donor ccf:sex ?sex .
    }
    OPTIONAL {
      ?donor ccf:bmi ?bmi .
    }
    OPTIONAL {
      ?donor ccf:race ?race .
    }
    OPTIONAL {
      ?dataset ccf:technology ?technology .
    }
    OPTIONAL {
      ?rui_location ccf:collides_with ?anatomical_structure .
    }
    OPTIONAL {
      ?rui_location ccf:collides_with ?anatomical_structure .
      ?cell_type ccf:ccf_located_in ?anatomical_structure .
    }
    OPTIONAL {
      ?rui_location ccf:collides_with ?anatomical_structure .
      ?biomarker ccf:biomarker_located_in ?anatomical_structure .
    }

#{{FILTER}}
  }

  ?rui_location rdf:type ccf:SpatialEntity ;
                ccf:x_dimension ?x_dimension ;
                ccf:y_dimension ?y_dimension ;
                ccf:z_dimension ?z_dimension ;
                ccf:dimension_unit ?dimension_units .
}
