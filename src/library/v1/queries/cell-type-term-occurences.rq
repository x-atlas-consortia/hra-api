PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX CCF: <https://purl.humanatlas.io/graph/ccf>
PREFIX DSGraphs: <https://purl.humanatlas.io/collection/ds-graphs>
PREFIX cell: <http://purl.obolibrary.org/obo/CL_0000000>

CONSTRUCT {
  ?cell_type ccf:count ?count .
}
FROM CCF:
FROM DSGraphs:
WHERE {
  SELECT DISTINCT ?cell_type (COUNT(DISTINCT(?block)) AS ?count)
  WHERE {
    ?block ccf:comes_from ?donor .
    ?block ccf:has_registration_location ?rui_location .
    OPTIONAL {
      {
        ?block ccf:generates_dataset ?dataset .
      }
      UNION
      {
        ?block ccf:subdivided_into_sections ?section .
        ?section ccf:generates_dataset ?sectionDataset .
      }
    }
    {
      ?rui_location ccf:collides_with ?anatomical_structure .
      ?cell_type ccf:ccf_located_in ?anatomical_structure . 
    }
    UNION
    {
      ?rui_location ccf:collides_with ?anatomical_structure .
      ?anatomical_structure ^ccf:ccf_located_in [
        ccf:ccf_ct_isa* ?cell_type
      ] .
    }
    UNION
    {
      BIND(cell: as ?cell_type)
    }
    #{{FILTER}}
  }
  GROUP BY ?cell_type
  ORDER BY DESC(?count)
}
