PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX CCF: <https://purl.humanatlas.io/graph/ccf>
PREFIX DSGraphs: <https://purl.humanatlas.io/collection/ds-graphs>

CONSTRUCT {
  ?biomarker ccf:count ?count .
}
FROM CCF:
FROM DSGraphs:
WHERE {
  SELECT DISTINCT ?biomarker (COUNT(DISTINCT(?block)) AS ?count)
  WHERE {
    ?block ccf:comes_from ?donor .
    ?block ccf:has_registration_location ?rui_location .
    {
      ?block ccf:generates_dataset ?dataset .
    }
    UNION
    {
      ?block ccf:subdivided_into_sections ?section .
      ?section ccf:generates_dataset ?sectionDataset .
    }
    ?rui_location ccf:collides_with ?anatomical_structure .
    ?biomarker ccf:ccf_bm_located_in ?anatomical_structure .
    #{{FILTER}}
  }
  GROUP BY ?biomarker
  ORDER BY DESC(?count)
}
