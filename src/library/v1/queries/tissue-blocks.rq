# This SPARQL query constructs a result set representing detailed information about tissue donors, samples, datasets, and sections.
# It combines data from the CCF ontology and experimental data sources.
#  
#  * Query Structure:
#     The query uses the CONSTRUCT statement to build a result set containing information about tissue donors, samples, datasets, and sections.
#     It retrieves data from the CCF ontology and external data sources.
#     The query constructs nodes for various entities, including tissue donors, samples, datasets, and sections.
#     It captures details such as donor type, tissue provider name, labels, comments, URLs, sex, age, BMI, technology, thumbnails,
#     section count, section size, and more.
#  * Note: The filter parameters (e.g., age, sex, BMI) can be added dynamically by the API user.
#          The string filter commented below serves as a placeholder and will be replaced with the appropriate filter conditions
#          when used in the API call.
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>

CONSTRUCT {
  ?tissueDonor rdf:type ?donorType ;
               ccf:tissue_provider_name ?tmc ;
               rdfs:label ?donorLabel ;
               rdfs:comment ?donorDescription ;
               ccf:url ?link .
  ?sample rdf:type ccf:Sample .
  ?sample ccf:sample_type ?sampleType ;
        rdfs:comment ?description;
    	  rdfs:label ?label;
        ccf:comes_from ?tissueDonor ;
        ccf:generates_dataset ?dataset ;
        ccf:subdivided_into_sections ?sections ;
        ccf:section_count ?sectionCount;
        ccf:section_size ?sectionSize;
        ccf:section_size_unit ?sectionUnit ;
    	  ccf:url ?link .
  
  ?dataset rdf:type ccf:Dataset ;
           ccf:technology ?technology ;
           ccf:thumbnail ?thumbnail ;
           rdfs:comment ?datasetDescription ;
           rdfs:label ?datasetLabel ;
    	   ccf:url ?link .
  ?sections rdf:type "Tissue Section";
    	  rdfs:label ?sectionLabel ;
    	  rdfs:comment ?sectionDescription ;
    	  ccf:url ?link .
  ?sample ccf:has_registration_location ?spatialEntityId .
}
FROM <https://purl.org/ccf/releases/2.2.1/ccf.owl> # Ontology / CCF.OWL
FROM <https://ccf-api.hubmapconsortium.org/v1/hubmap/rui_locations.jsonld> # Experimental data / data sets / rui locations
FROM <https://ccf-api.hubmapconsortium.org/v1/gtex/rui_locations.jsonld>

WHERE {
  ?sample rdf:type ccf:Sample .
  ?sample ccf:sample_type ?sampleType ; 
    	  ccf:has_registration_location ?spatialEntityId;
        ccf:generates_dataset ?dataset ;
  		  ccf:comes_from ?tissueDonor ;
        rdfs:comment ?description;
        ccf:section_count ?sectionCount;
        ccf:section_size ?sectionSize;
        ccf:section_size_unit ?sectionUnit ;
    	 rdfs:label ?label .
  ?spatialEntityId a ccf:SpatialEntity ;
                  ccf:collides_with ?annotation .
    ?cell_type ccf:ccf_located_in ?annotation .

  ?tissueDonor rdf:type ?donorType ;
              ccf:tissue_provider_name ?tmc ;
              rdfs:label ?donorLabel;
              rdfs:comment ?donorDescription;
              ccf:url ?link ;
              ccf:sex ?sex;
              ccf:age ?age;
              ccf:bmi ?bmi.

  ?dataset ccf:technology ?technology ;
           ccf:thumbnail ?thumbnail ;
           rdfs:comment ?datasetDescription ;
           rdfs:label ?datasetLabel .

  OPTIONAL {
    ?sections ccf:subdivided_into_sections ?sample;
        ccf:sample_type "Tissue Section" ;
  		  ccf:section_number ?sectionNumber ;
    	   rdfs:label ?sectionLabel ;
    	   rdfs:comment ?sectionDescription ;
    	   ccf:url ?link .
  }

  FILTER (?sampleType = "Tissue Block")
  FILTER (?donorType = ccf:Donor)
    #{{FILTER}}
  

}
