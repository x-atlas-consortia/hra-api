# This SPARQL query constructs a result set representing detailed information about tissue donors, samples, datasets, and sections.
# It combines data from the CCF ontology and experimental data sources.
#  
#  * Query Structure:
#     The query uses the CONSTRUCT statement to build a result set containing information about tissue donors, samples, datasets, and sections.
#     It retrieves data from the CCF ontology and external data sources.
#     The query constructs nodes for various entities, including tissue donors, samples, datasets, and sections.
#     It captures details such as donor type, tissue provider name, labels, comments, URLs, sex, age, BMI, technology, thumbnails,
#     section count, section size, and more.
#  * Note: The filter parameters (e.g., age, sex, BMI) can be added dynamically by the API user.
#          The string filter commented below serves as a placeholder and will be replaced with the appropriate filter conditions
#          when used in the API call.

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX CCF: <https://purl.humanatlas.io/graph/ccf>
PREFIX DSGraphs: <https://purl.humanatlas.io/collection/ds-graphs>

CONSTRUCT {
  ?donor rdf:type ccf:Donor ;
         ccf:sex ?sex ;
         ccf:age ?age ;
         ccf:bmi ?bmi ;
         ccf:consortium_name ?consortium ;
         ccf:tissue_provider_name ?provider ;
         rdfs:label ?donorLabel ;
         rdfs:comment ?donorDescription ;
         ccf:url ?link .
  ?sample rdf:type ccf:Sample ;
          ccf:sample_type ?sampleType ;
          ccf:has_registration_location ?rui_location ;
          ccf:subdivided_into_sections ?section ;
          ccf:comes_from ?donor ;
          ccf:generates_dataset ?dataset .
  ?section rdf:type ccf:Sample ;
            ccf:sample_type ?sectionType ;
            ccf:section_number ?sectionNumber ;
            rdfs:label ?sectionLabel ;
            rdfs:comment ?sectionDescription ;
            ccf:url ?sectionLink ;
            ccf:generates_dataset ?sectionDataset .
  ?dataset rdf:type ccf:Dataset ;
           ccf:technology ?technology ;
           ccf:thumbnail ?thumbnail ;
           rdfs:comment ?datasetDescription ;
           rdfs:label ?datasetLabel .
  ?sectionDataset rdf:type ccf:Dataset ;
                  ccf:technology ?sectionTechnology ;
                  ccf:thumbnail ?sectionThumbnail ;
                  rdfs:comment ?sectionDatasetDescription ;
                  rdfs:label ?sectionDatasetLabel .
  ?sample ccf:has_registration_location ?rui_location .
}
FROM CCF:
FROM DSGraphs:
WHERE {
  ?sample rdf:type ccf:Sample ;
          ccf:sample_type ?sampleType ;
          ccf:has_registration_location ?rui_location ;
          ccf:comes_from ?donor .
  ?donor rdf:type ccf:Donor ;
         ccf:sex ?sex ;
         ccf:consortium_name ?consortium ;
         ccf:tissue_provider_name ?provider ;
         rdfs:label ?donorLabel ;
         rdfs:comment ?donorDescription ;
         ccf:url ?link .

  OPTIONAL {
    ?rui_location ccf:collides_with ?anatomical_structure .
  }
  OPTIONAL {
    ?rui_location ccf:collides_with ?anatomical_structure .
    ?cell_type ccf:ccf_located_in ?anatomical_structure .
  }

  OPTIONAL {
    ?sample ccf:generates_dataset ?dataset . 
    ?dataset ccf:technology ?technology ;
           ccf:thumbnail ?thumbnail ;
           rdfs:comment ?datasetDescription ;
           rdfs:label ?datasetLabel .
  }
  
  OPTIONAL {
    ?sample ccf:subdivided_into_sections ?section .
    ?section rdf:type ccf:Sample ;
            ccf:sample_type ?sectionType ;
            ccf:section_number ?sectionNumber ;
            rdfs:label ?sectionLabel ;
            rdfs:comment ?sectionDescription ;
            ccf:url ?sectionLink 
            .
    
    OPTIONAL {
      ?section ccf:generates_dataset ?sectionDataset . 
      ?sectionDataset ccf:technology ?sectionTechnology ;
                ccf:thumbnail ?sectionThumbnail ;
                rdfs:comment ?sectionDatasetDescription ;
                rdfs:label ?sectionDatasetLabel .
    }
  }
  OPTIONAL { ?donor ccf:age ?age . }
  OPTIONAL { ?donor ccf:bmi ?bmi . }
  #{{FILTER}}
  FILTER (?sampleType = "Tissue Block")
}
