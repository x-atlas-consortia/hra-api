PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX CCF: <https://purl.humanatlas.io/graph/ccf>
PREFIX body: <http://purl.obolibrary.org/obo/UBERON_0013702>
PREFIX DSGraphs: <https://purl.humanatlas.io/collection/ds-graphs>

CONSTRUCT {
  ?anatomical_structure ccf:count ?count .
}
FROM CCF:
FROM DSGraphs:
WHERE {
  SELECT DISTINCT ?anatomical_structure (COUNT(DISTINCT(?block)) AS ?count)
  WHERE {
    ?block ccf:comes_from ?donor .
    ?block ccf:has_registration_location ?rui_location .
    OPTIONAL {
      {
        ?block ccf:generates_dataset ?dataset .
      }
      UNION
      {
        ?block ccf:subdivided_into_sections ?section .
        ?section ccf:generates_dataset ?sectionDataset .
      }
    }
    {
      ?rui_location ccf:collides_with ?anatomical_structure .
    }
    UNION
    {
      ?rui_location ccf:collides_with [
        ccf:ccf_part_of* ?anatomical_structure
      ] .
    }
    UNION
    {
      BIND(body: as ?anatomical_structure)
    }
    #{{FILTER}}
  }
  GROUP BY ?anatomical_structure
  ORDER BY DESC(?count)
}
