PREFIX ccf: <http://purl.org/ccf/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX hint: <http://www.bigdata.com/queryHints#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <http://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX LOD: <https://lod.humanatlas.io>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra-api>
PREFIX body: <http://purl.obolibrary.org/obo/UBERON_0013702>

CONSTRUCT {
  ?lod a dcat:Catalog ;
    dcat:last ?latestLodPurl ;
    rdfs:seeAlso ?graphPurl ;
    dct:title ?title ;
    schema:additionalType ?doType ;
    schema:name ?doName ;
    ccf:latestVersion ?doVersion ;
    schema:version ?version ;
    schema:dateCreated ?lastUpdated ;
    dcat:dataset ?lodPurl ;
    ccf:organ ?organ ;
    ccf:organ_id ?organ_id ;
    ccf:cell_count ?unique_cells ;
    ccf:biomarker_count ?unique_biomarkers .
}
WHERE {
  hint:Query hint:optimizer "Runtime" .

  GRAPH LOD: {
    ?lod a dcat:Catalog ;
      dcat:dataset ?lodPurl .
  
    ?lodPurl a dcat:Dataset ;
      schema:version ?version ;
      rdfs:seeAlso ?graphPurlVersioned .

    BIND(IRI(STRBEFORE(STR(?graphPurlVersioned), CONCAT('/', ?version))) as ?graphPurl)
  }

  {
    SELECT DISTINCT *
    WHERE {
      GRAPH ?graphPurl {
        ?graphPurl rdfs:seeAlso ?latestLodPurl .
      }

      GRAPH LOD: {
        # organ, unique_cells, unique_biomarkers
        ?lod a dcat:Catalog ;
          dcat:dataset ?latestLodPurl .
        
        ?latestLodPurl
          schema:additionalType ?doType ;
          schema:name ?doName ;
          schema:version ?doVersion ;
          prov:wasDerivedFrom [
            dct:title ?title ;
            schema:dateCreated ?lastUpdated ;
          ] .
      }
    }
  }

  OPTIONAL {
    SELECT DISTINCT ?graphPurl ?organ ?organ_id {
      {
        SELECT (SAMPLE(?organ) as ?organ) ?organ_id
        WHERE {
          GRAPH HRA: {
            ?refOrgan a ccf:SpatialEntity ;
                ccf:has_reference_organ ?refOrgan ;
                ccf:representation_of ?refOrganTerm .
            
            ?organ_id ccf:ccf_part_of body: .
            ?organ_id ccf:ccf_pref_label ?organ_label .
            ?refOrganTerm ccf:ccf_part_of* ?organ_id .
            FILTER(!CONTAINS(STR(?organ_label), ' system'))
          
            BIND(LCASE(?organ_label) as ?organ)
          }
        }
        GROUP BY ?organ_id
        ORDER BY ?organ
      }

      GRAPH ?graphPurl {
        ?graphPurl rdfs:seeAlso ?lodPurl .

        {
          ?organ_id ?pred1 [] .
        } UNION {
          [] ?pred2 ?organ_id .
          FILTER(isIRI(?organ_id))
        }
      }
    }
  }

  OPTIONAL {
    SELECT ?graphPurl (COUNT(DISTINCT ?cell_id) as ?unique_cells) {
      GRAPH HRA: {
        ?cell_id ccf:ccf_asctb_type ?type .
        FILTER(?type = 'CT')
      }

      GRAPH ?graphPurl {
        ?graphPurl rdfs:seeAlso ?lodPurl .

        {
          ?cell_id ?pred1 [] .
        } UNION {
          [] ?pred2 ?cell_id .
          FILTER(isIRI(?cell_id))
        }
      }
    }
    GROUP BY ?graphPurl
  }

  OPTIONAL {
    SELECT ?graphPurl (COUNT(DISTINCT ?biomarker) as ?unique_biomarkers) {
      GRAPH HRA: {
        ?biomarker ccf:ccf_asctb_type ?type .
        FILTER(?type = 'BM')
      }

      GRAPH ?graphPurl {
        ?graphPurl rdfs:seeAlso ?lodPurl .

        {
          ?biomarker ?pred1 [] .
        } UNION {
          [] ?pred2 ?biomarker .
          FILTER(isIRI(?biomarker))
        }
      }
    }
    GROUP BY ?graphPurl
  }
}